title: home
tags:
position: -487,-176
---
<<save_game>>
<<play_timeline 0201>>
<<play_sound "home" 1 true>>
<<wait  3>>
<<declare $num_to_reject = 0>>
<<set $num_to_reject = 0>>

<<jump Telephone_loop>>
===

title: Telephone_loop
position: -103,-208
---
<<play_sound "telephone_ring" 0.5 true>>
<<wait  1>>
<<set_animation_bool "SmallZ" "IsLookingLeft" true>>
    ->不接电话
        <<set $num_to_reject = $num_to_reject + 1>>
        <<set_animation_bool "SmallZ" "IsLookingLeft" false>>
        <<stop_sound "telephone_ring">>
        <<wait  2>>
        <<jump Telephone_loop>>
    ->接电话
        <<set_animation_bool "SmallZ" "IsLookingLeft" false>>
        <<play_timeline 0202>>
        <<jump Get_telephone>>
===

title: Get_telephone
position: -3,227
---
<<set_animation_bool "SmallZ" "IsStanding" true>>
<<stop_sound "telephone_ring">>
<<play_sound "phone_up" 1 false>>
    ->...
    ->喂？
<<if $num_to_reject > 0 && $num_to_reject < 2>>
    父亲:为什么不接我的电话？
        ->就是不想接
            父亲: ...
            父亲: 埃里克，<shake a=0.5><notype>你小子是不是欠揍？</notype></shake>
            父亲: 等我回来再收拾你。
        ->我在写作业
            父亲: 你觉得我会信吗？
                ->你为什么不信
                ->我真的在写
                父亲:哼，你要是真在写，我就不会接到那么多从你学校打来的电话！
<<elseif $num_to_reject > 3>>
    父亲: 你他妈是腿断了还是耳朵聋了？
    父亲: 你知道我打了多少个电话吗？   
        ->{$num_to_reject + 1}个
            父亲: 好，看起来你小子数学还不错啊？
            父亲: 那你他妈的可以把你他妈的作业给我写了吗？
            父亲: 我已经受够了从你学校打来的电话了。
        ->我不知道
            父亲: 埃里克，<shake a=0.5> <color=red>我警告你不要挑战我的底线。<shake a=0.5> <color=red>

<<else>>
父亲: 臭小子，怎么这么久才接电话？
<<endif>>

父亲: 听好了，今晚工厂加班，不回来。
父亲: 你自己乖乖留在家里，不准出门。
父亲: 如果隔壁那个烦人的小兔崽子又跑过来，不准让他在我的沙发上打滚。
父亲: 晚饭自己做，别饿死了。
父亲: 还有一件事，
父亲: 如果再因为你没有写作业，学校的电话打到我这里来，
父亲: <shake a=0.5> <color=red> <notype>我一定好好收拾你。</notype> <shake a=0.5> <color=red>
<<play_sound "phone_down" 1 false>>

<<wait 3>>
<<set_animation_bool "SmallZ" "IsStanding" false>>
<<play_timeline 0203>>

<<wait  2>>
（要写作业吗？）
    ->不写
        <<wait  5>>
        （真的决定不写作业吗？）
            ->真的
                <<wait  5>>
                （这样真的可以吗？）
                    ->就是不写
                        <<wait  5>>
                        （要不还是写一下吧）
                            ->不
                                <<wait  5>>
                                （其实这样没有好的下场吧）
                                    ->不管
                                        <<wait  5>>
                                        （你这样会后悔的）
                                            ->我不后悔
                                                <<wait  5>>
                                                <<set_volume "home" 0>>
                                                <<play_timeline  0204>>
                                                <<wait  3>>
                                                <<set_volume "home" 1>>
                                                （写作业。）
                                                    ->好的
                                                    ->好的
                                                <<jump do_homework>>
                                            ->好吧，我认输了
                                                <<jump do_homework>>
                                    ->好吧，写一下
                                        <<jump do_homework>>
                            ->好吧
                                <<jump do_homework>>
                    ->还是写一下吧
                        <<jump do_homework>>
            ->还是写一下吧
                <<jump do_homework>>
    ->写
        <<jump do_homework>>
===

title: do_homework
position: 310,-136
---
...
第一题。
“这句话是假的。”
请问这句话是真的还是假的？
<<declare $hw1 = false>>
（这是什么...题？）
    ->假的
        <<set $hw1 = false>>
    ->真的
        <<set $hw1 = true>>
（这样选...真的对吗？）
    ->假的<<if $hw1 == true>>
    ->真的<<if $hw1 == false>>
（是...这样就对吗？）
（不管了，先看下一题吧）
（...）

第二题。
“一位理发师宣称：‘我只给不自己刮胡子的人刮胡子。’
那么，他该不该给自己刮胡子？”
<<declare $hw2 = false>>
（这...题是什么意思？）
    ->不该
        <<set $hw2 = false>>
    ->该
        <<set $hw2 = true>>
（等等。好像不对...）
    ->不该<<if $hw2 == true>>
    ->该<<if $hw2 == false>>
（好像...不对？）
（如果他给自己刮胡子，那他就不该给自己刮胡子。）
（如果他不给自己刮胡子，那他就该给自己刮胡子。）
（这...到底该不该刮？）
（...）
（先看下一题吧）

第三题。
“一辆失控的有轨电车在轨道上疾驰，前方的轨道上绑着5个人。
你站在轨道旁的控制杆旁。
如果拉动控制杆，电车将切换到另一条轨道上，
但那条轨道上也绑着1个人。
你会拉动控制杆吗？”
<<declare $hw3 = false>>
（...?）
    ->按
        <<set $hw3 = true>>
    ->不按
        <<set $hw3 = false>>

小孩:哥哥！
<<set_animation_bool "SmallZ" "IsLookingRight" true>>
<<play_timeline 0205>>
小孩:哥哥！我们出去玩吧！
    ->不去
        小孩:为什么？
            ->就是不想去
                <<jump handle_with_kids>>
            ->我要写作业
                小孩:哥哥，从来没听说你要写作业。
                    ->你好烦。
                        <<jump handle_with_kids>>
                    ->你管那么多干什么？
                        <<jump handle_with_kids>>
    ->我很忙
        小孩:你在忙什么？
            ->你好烦
                <<jump handle_with_kids>>
            ->写作业
                <<jump handle_with_kids>>
===

title: handle_with_kids
position: 428,200
---
<<set_animation_bool "SmallZ" "IsLookingRight" false>>
小孩:哥哥，你真没意思。

<<play_timeline 0206>>
<<set_animation_bool "SmallZ" "IsLookingLeft" true>>
小孩:哥哥，你知道海边是什么样子吗？
    ->赶紧给我从沙发上下来！
        小孩:我不。除非你回答我的问题。
            ->那你随意
            ->好吧，我不知道
    ->不知道
小孩:我爸爸说，以前的海边有好多好多的贝壳，
小孩:有二极管一样蓝的天空和水。
小孩:还有好多好多的人。
小孩:他们都在沙滩上晒太阳，
小孩:还有人在海里游泳。
<<set_animation_bool "SmallZ" "IsLookingLeft" false>>
小孩:哥哥，你见过大海吗？
    ->我没兴趣
    ->那里很危险
<<play_timeline 0207>>

小孩:难道你真的不想去看看吗？
    ->不想
    ->想
        小孩:那我们一起去吧！
            ->我得写作业
            ->我不能出去
小孩:哥哥，你真没意思。
小孩:不会是因为怕你爸吧？
<<set_animation_bool "SmallZ" "IsLookingLeft" true>>
    ->给我闭嘴
    ->滚出去
小孩:切。就是。
小孩:胆小鬼。
    ->滚出去
    ->滚出去
小孩:你不去就我自己去，拜拜。
<<set_animation_bool "SmallZ" "IsLookingLeft" false>>
<<play_timeline 0208>>

...
第四题。
“你是谁？”
（这是什么作业...?）
    ->埃里克·坎贝尔
    ->Z
<<stop_sound_fade "home" 1>>
<<play_sound "Begin_rain" 1 true>>
<<play_timeline 0209>>
<<wait 5>>
（这么大的雨...）
（海边很危险！）
<<declare $LengMo = false>>
（他不会有什么事吧？）
    -> 不闻不问
        <<wait 5>>
            （不行，这样下去会出事的。）
                -> 死了更好
                    <<set $LengMo = true>>
                    <<wait 5>>
                    （...）
                        ->去看看
                -> 去看看
    -> 去看看
<<play_timeline 0210>>
<<stop_sound_fade "Begin_rain" 2>>
<<change_scene Level3_seaside seaside>>
===