title: classroom
---
<<play_timeline 0501>>//亮灯
<<play_sound "classroom" 1 true>>
老师: 上节课我们讲了拉普拉斯信条。
老师: 现在来进行提问。
老师: 让我看看...
老师: 埃里克·坎贝尔。
<<declare $Silent_Count = 0>>
    -> 沉默
        <<set $Silent_Count = $Silent_Count + 1>>
        老师: 埃里克·坎贝尔?
            -> 沉默
                <<set $Silent_Count = $Silent_Count + 1>>
                老师: 奇怪。
                老师: 这里没有叫埃里克·坎贝尔的学生吗？
                    -> 沉默
                        <<set $Silent_Count = $Silent_Count + 1>>
                        老师: ...好吧。
                        老师: 埃里克·坎贝尔记旷课。我们换一个人。
                        老师: 让我看看...
                        <<jump Saven_Elliot>>
                    -> <wiggle a=0.3 f=2>站起来</wiggle>                           
            -> <wiggle a=0.4 f=1.5>站起来</wiggle>   
    -> <wiggle a=0.5 f=1>站起来</wiggle>
    <<jump Stand_Up>>
===

title: Stand_Up
---
<<set_animation_bool "StudentZ" "IsStanding" true>>
<<wait 1>>
<<declare $Is_Z = false>>
老师: 你是埃里克·坎贝尔?
    -> 不是。
        老师: 那你为什么站起来？
            -> 有人想让我站起来
                老师: ..谁？
                // 设置灯带使用渐变脉冲效果
                <<set_strip_pulse_effects "gradient" "default">>
                // 设置0号灯带渐变参数：从红色到绿色，持续2500毫秒
                <<set_gradient_pulse_params 0 "#FF001E" "#00FF1E" 2500>>
                <<set_button_mapping 1 1>>
                <<set_selection_delay 3>>
                    -> Z
                    -> 我不知道
                <<set_strip_pulse_effects "default" "default">>
                <<set_button_mapping 0 1>>
                <<set_selection_delay 0.5>>
                老师: ...？
                老师: 这位同学，如果你是来捣乱的，<color=red><notype><shake a=0.5>请离开教室</shake></notype></color>。
                老师: 告诉我你叫什么。
                    -> Z
                        <<set $Is_Z = true>>
                        老师: Z？我从来没有听过这种愚蠢的名字。
                        老师: <rainb>你难道是什么实验小白鼠吗？</rainb>
                        老师: <wave>又或者自以为身处什么科幻电影中？</wave>
                        老师: 我再给你一次机会。你叫什么？
                            -> 123456789Z <<if $Is_Z == false>>
                            -> 埃里克·坎贝尔
                    -> 埃里克·坎贝尔
                老师: 我一点也不觉得这很幽默。你的喜剧天分烂透了。
                老师: 埃里克·坎贝尔，我觉得我有义务提醒你。
                老师: 如果不想毕业后只能去污水处理厂工作，请认真上课。
            -> 我想回答这个问题
                老师: 好的。那你的名字是？
                    -> Z
                        <<set $Is_Z = true>>
                        老师: Z？你认真的吗？
                        老师: 这听起来像是什么实验小白鼠的代号。
                        老师: <wave>或者你是看科幻小说看入迷了吗？</wave>
                        老师: 好吧，暂且不管这个奇怪的名字,很高兴你愿意回答这个问题。
                        老师: Z同学，请你说说吧。
                    -> 埃里克·坎贝尔
                        老师: ？
                        老师: 我不觉得这很幽默。
                        老师: 埃里克·坎贝尔，我觉得我有义务提醒你。
                        老师: 如果不想毕业后只能去污水处理厂工作，请认真上课。   
                        老师: 现在回答一下这个问题。      
    -> 是。
        <<if $Silent_Count == 0>>
        老师: 好。你来说说。
        <<else>>
        老师: 埃里克·坎贝尔，我觉得我有义务提醒你。
        老师: 如果不想毕业后只能去污水处理厂工作，请专心上课。 
        老师: 现在回答一下这个问题。 
        <<endif>>
老师: 拉普拉斯信条的内容是？
<<declare $Is_Answer_Right = false>>
拉普拉斯信条是一种...
    -> 决定论
        是一种决定论,认为宇宙中所有事件都是由...
            -> 概率事件决定
                由概率事件决定,人的一切活动都是...
                    -> 可预知的
                    -> 不可预知的
            -> 初始条件决定
                由初始条件决定,人的一切活动都是...
                    -> 可预知的
                        <<set $Is_Answer_Right = true>>
                    -> 不可预知的
    -> 不可知论
        是一种不可知论,认为宇宙中所有事件都是由...
            -> 高级文明预设
                由高级文明预设,人的一切活动都是...
                    -> 可预知的
                    -> 不可预知的
            -> 自由意志决定
                由自由意志决定,人的一切活动都是...
                    -> 可预知的
                    -> 不可预知的
<<if $Is_Answer_Right == false>>
    老师: <color=red>完全错误。</color>
    老师: 拉普拉斯信条，是一种决定论。
    老师: 认为宇宙中所有事件都是由初始条件决定，所有行为可以预测。
    老师: 除非你不想通过考试，否则请认真上课。
<<else>>
    老师: 好吧，回答正确。
<<endif>>
<<set_animation_bool "StudentZ" "IsStanding" false>>
<<wait 1>>
老师: 现在我们换一个人。
老师: ...
<<jump Saven_Elliot>>
===

title: Saven_Elliot
---
老师: 莎文·<color=red>埃雷特</color> 。
（...）
老师: 莎文·埃雷特？
<<wait 2>>
<<set_animation_bool "Saven" "IsStanding" true>>
<<wait 1>>
<<if $Silent_Count == 3>>
    老师: 拉普拉斯信条的内容是？
    莎文: ...
    莎文: 拉普拉斯信条是一种决定论。
    莎文: 宇宙中所有事件都已安排好。
    莎文: 就像
<<else>>
    老师: 拉普拉斯信条是否正确？
    莎文: ...
    莎文: 正确。
    老师: 错误。
    莎文: 不，正确。
    老师: 你什么意思？
    莎文: 宇宙中所有事件都已安排好。
    莎文: 就像
<<endif>>
<<declare $current_time = "">>
<<get_time "current_time" "HH:mm">>
莎文: 现在是{$current_time}
<<stop_sound "classroom">>
莎文: <color=red>而<waitfor=0.2>我<waitfor=0.2>会<waitfor=0.2>在<waitfor=0.2>五<waitfor=0.2>分<waitfor=0.2>钟<waitfor=0.2>后<waitfor=0.2>死<waitfor=0.2>去。</color>
<<start_countdown 300 "death_scene">>
<<play_sound "strange" 1 true>>
<<wait  3>>
同学1: 这个神经病又在发什么疯？
同学2: 谁知道？从她转过来开始就没正常过。
同学3: 好奇怪...
同学2: 装疯卖傻。
同学1: 可别一会真死了。
老师: 安静！
老师: 莎文·埃雷特，我希望你认识到，作为转学生，学校接受你很大程度是出于你父母的关系。
老师: 但这不是你在课堂上胡言乱语的理由。
莎文: <color=red><shake a=0.5>我不是转学生。他们不是我的父母。我没有选择的机会。一切都是安排好的。</shake></color>
老师: 什么？
莎文: <color=red><shake a=0.5>一切都是安排好的。我马上就会死。我将不得不死。任何事情都是安排好的，他正看着我呢！</shake></color>
老师: 你到底在说什么？谁在看着你？
莎文: <color=red><shake a=0.5>我不知道！我不知道！我不知道！我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道我不知道</shake></color>
老师: 够了！！！
老师: 滚出去。
老师: 听见了吗？给我滚出去！！！
<<play_timeline 0502>>//莎文离开
<<wait 3>>
老师: 我们继续上课。
老师: 拉普拉斯信条是错误的。
老师: 量子力学的不确定性原理证明微观粒子行为本质随机，
老师: 混沌理论显示初始条件微变会导致宏观系统完全无法预测，
老师: 哥德尔不完备定理则从数学上宣告了认知的绝对边界。
// 老师: 宇宙是确定性的，但也是随机的。
// 老师: 我们无法预测未来，但我们可以选择如何面对未来。
老师: 只有一种情况，拉普拉斯信条是正确的。
老师: <color=blue>那就是，你认为你是被圈养的存在。</color>
老师: 相信这种观点的人，也不必出现在这个学校里了。
<<stop_sound_fade "strange"1>>
<<play_sound "classover" 1 false>>//下课
<<wait 3>>
老师: 下课。
<<play_timeline 0503>>//熄灯又开灯，同学换姿势，老师出去
<<jump After_Class>>
===

title: After_Class
---
<<play_sound "classroom" 1 true>>
->什么也不做
    ...
    （莎文·埃雷特。）
    （他们是什么关系？）
    ...
    （不。谁都知道他从小就是个独生子。）
    （不。）
    （不可能。）
    （不可能！）
    同学2: 那个神经病终于消失了。
    同学3: 这种人怎么能到学校来。
    同学2: 你没听老师说吗？她父母有关系。
    同学3: 学校真是什么人都要。
    同学2: 偷偷告诉你，我觉得她一定有问题。
    同学3: 这不是废话吗？
    同学2: 我不是这个意思。是她的手臂。
    同学3: 怎么了？
    同学2: 针孔。密密麻麻的针孔。
    同学2: 像蜂窝一样，没有一片皮肤是好的。
    同学2: 我上厕所的时候看到的。
    同学2: 她一定在做什么见不得人的事情。
    同学3: 天呐···
    ···
    ->继续什么也不做
        同学2: 那边那个呆子也不是什么好东西。
        同学3: 哪个？
        同学2: 上课回答问题的那个。
        同学3: 嘘！
        同学3: 他还坐在那里呢！
        同学2: 放心，他肯定听不到的。
        同学3: 他怎么了？
        同学2: 你知道他成绩有多烂吧。
        同学3: 全世界都知道。
        同学2: 你猜他怎么进到这所学校的？
        同学3: 不会也是...?
        同学2: 猜对了一半。
        同学2: 他爸以前是污水处理厂的工人。
        同学2: 后来得了<color=red>血锈病</color>。
        同学3: ···！
        同学2: 污水处理厂以前也有过工人得血锈病。
        同学2: 以往出现这种情况都是赔一大笔钱了事。
        同学2: 但是你也知道，血绣病几乎不可能治好。
        同学2: 他爸知道自己治不好病，就去跟厂长谈条件。
        同学3: 污水处理厂的厂长是...
        同学2: 对！是学校的董事。
        同学3: 原来是踩着父亲的命上来的。
        同学2: 可不是，但你看他那个样子。上学有什么用？
        同学3: 你怎么知道这么多？
        同学2: 嘘！
        同学2: 我爸是董事会的。我偷偷看过他的文件袋。
        同学3: 我擦，那你也太厉害了！
        同学2: 这件事可不准说出去。
        同学3: 明白明白！
        ...
        <<set_button_mapping 1 1>>
        <<set_option_colors "#00FF1E" "#00FF1E">>
        ->继续什么也不做
        ->出去看看
        <<set_button_mapping 0 1>>
        <<set_option_colors "#FF001E" "#00FF1E">>
    ->出去看看
->出去看看
<<play_timeline 0505>>//男主跑出去
<<stop_sound_fade "classroom" 2>>
<<play_timeline 0504>> //熄灯
<<change_scene Level6_corridor corridor>>
===


